[[algorithms-astar]]
= A* Shortest Path
:description: This section describes the A* Shortest Path algorithm in the Neo4j Graph Data Science library.
:entity: source-target-pair
:result: shortest path
:algorithm: A*
:source-target: true
:procedure-name: pass:q[gds.shortestPath.astar]


:directed:
:undirected:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


[[algorithms-astar-intro]]
== Introduction

The A* (pronounced "A-Star") Shortest Path algorithm computes the shortest path between two nodes.
A* is an informed search algorithm as it uses a heuristic function to guide the graph traversal.
The algorithm supports weighted graphs with positive relationship weights.

Unlike xref:algorithms/dijkstra-source-target.adoc[Dijkstra's shortest path algorithm], the next node to search from is not solely picked on the already computed distance.
Instead, the algorithm combines the already computed distance with the result of a heuristic function.
That function takes a node as input and returns a value that corresponds to the cost to reach the target node from that node.
In each iteration, the graph traversal is continued from the node with the lowest combined cost.

In GDS, the A* algorithm is based on the xref:algorithms/dijkstra-source-target.adoc[Dijkstra's shortest path algorithm].
The heuristic function is the haversine distance, which defines the distance between two points on a sphere.
Here, the sphere is the earth and the points are geo-coordinates stored on the nodes in the graph.

The algorithm implementation is executed using a single thread.
Altering the concurrency configuration has no effect.


[[algorithms-astar-requirements]]
== Requirements

In GDS, the heuristic function used to guide the search is the https://en.wikipedia.org/wiki/Haversine_formula[haversine formula].
The formula computes the distance between two points on a sphere given their longitudes and latitudes.
The distance is computed in nautical miles.

In order to guarantee finding the optimal solution, i.e., the shortest path between two points, the heuristic must be admissible.
To be admissible, the function must not overestimate the distance to the target, i.e., the lowest possible cost of a path must always be greater or equal to the heuristic.

This leads to a requirement on the relationship weights of the input graph.
Relationship weights must represent the distance between two nodes and ideally scaled to nautical miles.
Kilometers or miles also work, but the heuristic works best for nautical miles.


[[algorithms-astar-syntax]]
== Syntax

include::partial$/algorithms/shortest-path/path-syntax.adoc[]


[[algorithms-astar-examples]]
== Examples

:algorithm-name: {algorithm}
:graph-description: transport network
:image-file: astar.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Station {name: 'Kings Cross',         latitude: 51.5308, longitude: -0.1238}),
       (b:Station {name: 'Euston',              latitude: 51.5282, longitude: -0.1337}),
       (c:Station {name: 'Camden Town',         latitude: 51.5392, longitude: -0.1426}),
       (d:Station {name: 'Mornington Crescent', latitude: 51.5342, longitude: -0.1387}),
       (e:Station {name: 'Kentish Town',        latitude: 51.5507, longitude: -0.1402}),
       (a)-[:CONNECTION {distance: 0.7}]->(b),
       (b)-[:CONNECTION {distance: 1.3}]->(c),
       (b)-[:CONNECTION {distance: 0.7}]->(d),
       (d)-[:CONNECTION {distance: 0.6}]->(c),
       (c)-[:CONNECTION {distance: 1.3}]->(e)
----

The graph represents a transport network of stations.
Each station has a geo-coordinate, expressed by `latitude` and `longitude` properties.
Stations are connected via connections.
We use the `distance` property as relationship weight which represents the distance between stations in kilometers.
The algorithm will pick the next node in the search based on the already traveled distance and the distance to the target station.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
    'myGraph',
    'Station',
    'CONNECTION',
    {
        nodeProperties: ['latitude', 'longitude'],
        relationshipProperties: 'distance'
    }
)
----

In the following example we will demonstrate the use of the {algorithm} Shortest Path algorithm using this graph.


[[algorithms-astar-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in write mode:
[source, cypher, role=noplay]
----
MATCH (source:Station {name: 'Kings Cross'}), (target:Station {name: 'Kentish Town'})
CALL gds.shortestPath.astar.write.estimate('myGraph', {
    sourceNode: source,
    targetNode: target,
    latitudeProperty: 'latitude',
    longitudeProperty: 'longitude',
    writeRelationshipType: 'PATH'
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
RETURN nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 5         | 5                 | 1016      | 1016      | "1016 Bytes"
|===
--


[[algorithms-astar-examples-stream]]
=== Stream

:!stream-details:
include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
MATCH (source:Station {name: 'Kings Cross'}), (target:Station {name: 'Kentish Town'})
CALL gds.shortestPath.astar.stream('myGraph', {
    sourceNode: source,
    targetNode: target,
    latitudeProperty: 'latitude',
    longitudeProperty: 'longitude',
    relationshipWeightProperty: 'distance'
})
YIELD index, sourceNode, targetNode, totalCost, nodeIds, costs, path
RETURN
    index,
    gds.util.asNode(sourceNode).name AS sourceNodeName,
    gds.util.asNode(targetNode).name AS targetNodeName,
    totalCost,
    [nodeId IN nodeIds | gds.util.asNode(nodeId).name] AS nodeNames,
    costs,
    nodes(path) as path
ORDER BY index
----

.Results
[opts="header"]
|===
| index | sourceNodeName | targetNodeName | totalCost | nodeNames                                        | costs                | path
| 0     | "Kings Cross"  | "Kentish Town" | 3.3       | [Kings Cross, Euston, Camden Town, Kentish Town] | [0.0, 0.7, 2.0, 3.3] | [Node[0], Node[1], Node[2], Node[4]]
|===
--

The result shows the total cost of the shortest path between node `King's Cross` and `Kentish Town` in the graph.
It also shows ordered lists of node ids that were traversed to find the shortest paths as well as the accumulated costs of the visited nodes.
This can be verified in the xref:algorithms/astar.adoc#algorithms-astar-examples[example graph].
Cypher Path objects can be returned by the `path` return field.
The Path objects contain the node objects and virtual relationships which have a `cost` property.

[[algorithms-astar-examples-mutate]]
=== Mutate

include::partial$/algorithms/shortest-path/path-examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher, role=noplay]
----
MATCH (source:Station {name: 'Kings Cross'}), (target:Station {name: 'Kentish Town'})
CALL gds.shortestPath.astar.mutate('myGraph', {
    sourceNode: source,
    targetNode: target,
    latitudeProperty: 'latitude',
    longitudeProperty: 'longitude',
    relationshipWeightProperty: 'distance',
    mutateRelationshipType: 'PATH'
})
YIELD relationshipsWritten
RETURN relationshipsWritten
----

.Results
[opts="header"]
|===
| relationshipsWritten
| 1
|===
--

After executing the above query, the in-memory graph will be updated with new relationships of type `PATH`.
The new relationships will store a single property `totalCost`.

[NOTE]
====
The relationship produced is always directed, even if the input graph is undirected.
====


[[algorithms-astar-examples-write]]
=== Write

include::partial$/algorithms/shortest-path/path-examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher, role=noplay]
----
MATCH (source:Station {name: 'Kings Cross'}), (target:Station {name: 'Kentish Town'})
CALL gds.shortestPath.astar.write('myGraph', {
    sourceNode: source,
    targetNode: target,
    latitudeProperty: 'latitude',
    longitudeProperty: 'longitude',
    relationshipWeightProperty: 'distance',
    writeRelationshipType: 'PATH',
    writeNodeIds: true,
    writeCosts: true
})
YIELD relationshipsWritten
RETURN relationshipsWritten
----

.Results
[opts="header"]
|===
| relationshipsWritten
| 1
|===
--

The above query will write one relationship of type `PATH` back to Neo4j.
The relationship stores three properties describing the path: `totalCost`, `nodeIds` and `costs`.

[NOTE]
====
The relationship written is always directed, even if the input graph is undirected.
====
