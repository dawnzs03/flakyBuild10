[[alpha-algorithms-linkprediction-preferential-attachment]]
[.alpha]
= Preferential Attachment
:description: This section describes the Preferential Attachment algorithm in the Neo4j Graph Data Science library.


Preferential Attachment is a measure used to compute the closeness of nodes, based on their shared neighbors.

include::partial$/operations-reference/alpha-note.adoc[]


[[algorithms-linkprediction-preferential-attachment-context]]
== History and explanation

Preferential attachment means that the more connected a node is, the more likely it is to receive new links.
This algorithm was popularised by https://en.wikipedia.org/wiki/Barab%C3%A1si%E2%80%93Albert_model[Albert-László Barabási and Réka Albert^] through their work on scale-free networks.
It is computed using the following formula:

image::equations/preferential-attachment.svg[role="middle"]

where `N(u)` is the set of nodes adjacent to `u`.

A value of 0 indicates that two nodes are not close, while higher values indicate that nodes are closer.

The library contains a function to calculate closeness between two nodes.


[[algorithms-linkprediction-preferential-attachment-syntax]]
== Syntax

.The following will run the algorithm and return the result:
[source, cypher, role=noplay]
----
RETURN gds.alpha.linkprediction.preferentialAttachment(node1:Node, node2:Node, {
    relationshipQuery:String,
    direction:String
})
----


.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name                   | Type    | Default        | Optional | Description
| `node1`                | Node    | null           | no       | A node
| `node2`                | Node    | null           | no       | Another node
| `relationshipQuery`    | String  | null           | yes      | The relationship type used to compute similarity between `node1` and `node2`
| `direction`            | String  | BOTH           | yes      | The relationship direction used to compute similarity between `node1` and `node2`. Possible values are `OUTGOING`, `INCOMING` and `BOTH`.
|===


[[algorithms-linkprediction-preferential-attachment-sample]]
== Preferential Attachment algorithm sample

.The following will create a sample graph:
[source, cypher, role=noplay]
----
CREATE
 (zhen:Person {name: 'Zhen'}),
 (praveena:Person {name: 'Praveena'}),
 (michael:Person {name: 'Michael'}),
 (arya:Person {name: 'Arya'}),
 (karin:Person {name: 'Karin'}),

 (zhen)-[:FRIENDS]->(arya),
 (zhen)-[:FRIENDS]->(praveena),
 (praveena)-[:WORKS_WITH]->(karin),
 (praveena)-[:FRIENDS]->(michael),
 (michael)-[:WORKS_WITH]->(karin),
 (arya)-[:FRIENDS]->(karin)
----

.The following will return the Preferential Attachment score for Michael and Karin:
[source, cypher, role=noplay]
----
 MATCH (p1:Person {name: 'Michael'})
 MATCH (p2:Person {name: 'Karin'})
 RETURN gds.alpha.linkprediction.preferentialAttachment(p1, p2) AS score
----

.Results
[opts="header",cols="1"]
|===
| `score`
| 6.0
|===


We can also compute the score of a pair of nodes based on a specific relationship type.

.The following will return the Preferential Attachment score for Michael and Karin based only on the `FRIENDS` relationship:
[source, cypher, role=noplay]
----
 MATCH (p1:Person {name: 'Michael'})
 MATCH (p2:Person {name: 'Karin'})
 RETURN gds.alpha.linkprediction.preferentialAttachment(p1, p2, {relationshipQuery: "FRIENDS"}) AS score
----

.Results
[opts="header",cols="1"]
|===
| `score`
| 1.0
|===
