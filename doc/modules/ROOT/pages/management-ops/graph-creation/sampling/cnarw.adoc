[[algorithms-cnarw]]
[.beta]
= Common Neighbour Aware Random Walk sampling
:description: This section describes the Common Neighbour Aware Random Walk sampling algorithm in the Neo4j Graph Data Science library.
:page-aliases: management-ops/projections/cnarw.adoc

include::partial$/operations-reference/beta-note.adoc[]

:directed:
:undirected:
:homogeneous:
:heterogeneous:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]

== Introduction

Graph sampling algorithms can be used to reduce the size of large and complex graphs while preserving structural properties.
This can help to reduce bias, and ensure privacy, and making graph analysis more scalable.
Sampling algorithms are widely used in machine learning, social network analysis, and many other applications.

The Common Neighbour Aware Random Walk (CNARW) is a graph sampling technique that involves optimizing the selection of the next-hop node.
It takes into account the number of common neighbours between the current node and the next-hop candidates.

According to the paper, a major reason why simple random walks tend to converge slowly is due to the high clustering feature that is typical for some kinds of graphs e.g. for online social networks (OSNs).
When moving to neighbours uniformly at random, it is easy to get caught in local loops and revisit previously visited nodes, which slows down convergence.

To address this issue, one solution is to prioritize nodes that offer a higher likelihood of exploring unvisited nodes in each step.
Nodes with higher degrees may provide this opportunity, but they may also have more common neighbours with previously visited nodes, increasing the likelihood of revisits.

Therefore, choosing a node with a higher degree and fewer common neighbours with previously visited nodes (or the current node) not only increases the chances of discovering unvisited nodes but also reduces the probability of revisiting previously visited nodes in future steps.

The implementation of the algorithm is based on the following paper:

* https://ieeexplore.ieee.org/abstract/document/9712235[Common Neighbors Matter: Fast Random Walk Sampling with Common Neighbor Awareness^]


=== Relationship weights

Same as in the xref:management-ops/graph-creation/sampling/rwr.adoc#alpha-algorithms-rwr-syntax-configuration[`relationshipWeightProperty`] parameter in `RandomWalksWithRestarts` algorithm.

=== Node label stratification

Same as in the xref:management-ops/graph-creation/sampling/rwr.adoc#alpha-algorithms-rwr-syntax-configuration[`nodeLabelStratification`] parameter in `RandomWalksWithRestarts` algorithm.

== Syntax

[.project-sample-syntax]
--
.The following describes the API for running the algorithm
[source, cypher, role=noplay]
----
CALL gds.graph.sample.cnarw(
  graphName: String,
  fromGraphName: String,
  configuration: Map
)
YIELD
  graphName,
  fromGraphName,
  nodeCount,
  relationshipCount,
  startNodeCount,
  projectMillis
----

.Parameters
[opts="header",cols="1,1,4"]
|===
| Name                | Type    | Description
| graphName           | String  | The name of the new graph that is stored in the graph catalog.
| fromGraphName       | String  | The name of the original graph in the graph catalog.
| configuration       | Map     | Additional parameters to configure the subgraph sampling.
|===

[[algorithms-cnarw-syntax-configuration]]
.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name                    | Type            | Default                           | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
| xref:common-usage/running-algos.adoc#common-configuration-relationship-weight-property[relationshipWeightProperty] | String  | null    | yes       | Name of the relationship property to use as weights. If unspecified, the algorithm runs unweighted.
| samplingRatio           | Float           | 0.15                              | yes      | The fraction of nodes in the original graph to be sampled.
| restartProbability      | Float           | 0.1                               | yes      | The probability that a sampling random walk restarts from one of the start nodes.
| startNodes              | List of Integer | A node chosen uniformly at random | yes      | IDs of the initial set of nodes of the original graph from which the sampling random walks will start.
| nodeLabelStratification | Boolean         | false                             | yes      | If true, preserves the node label distribution of the original graph.
| randomSeed              | Integer         | n/a                               | yes      | The seed value to control the randomness of the algorithm.
Note that `concurrency` must be set to 1 when setting this parameter.
|===

.Results
[opts="header",cols="1,1,4"]
|===
| Name                   | Type     | Description
| graphName              | String   | The name of the new graph that is stored in the graph catalog.
| fromGraphName          | String   | The name of the original graph in the graph catalog.
| nodeCount              | Integer  | Number of nodes in the subgraph.
| relationshipCount      | Integer  | Number of relationships in the subgraph.
| startNodeCount         | Integer  | Number of start nodes actually used by the algorithm.
| projectMillis          | Integer  | Milliseconds for projecting the subgraph.
|===
--


== Examples

In this section we will demonstrate the usage of the CNARW sampling algorithm on a small toy graph.


=== Setting up

:algorithm-name: Common Neighbour Aware Random Walk graph sampling
:graph-description: social network
:image-file: graph-rj-2-k5.svg
include::partial$/algorithms/shared/examples-intro.adoc[]


.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
    (J:Female {name:'Juliette'}),
    (R:Male {name:'Romeo'}),
    (r1:Male {name:'Ryan'}),
    (r2:Male {name:'Robert'}),
    (r3:Male {name:'Riley'}),
    (r4:Female {name:'Ruby'}),
    (j1:Female {name:'Josie'}),
    (j2:Male {name:'Joseph'}),
    (j3:Female {name:'Jasmine'}),
    (j4:Female {name:'June'}),
    (J)-[:LINK]->(R),
    (R)-[:LINK]->(J),
    (r1)-[:LINK]->(R),   (R)-[:LINK]->(r1),
    (r2)-[:LINK]->(R),   (R)-[:LINK]->(r2),
    (r3)-[:LINK]->(R),   (R)-[:LINK]->(r3),
    (r4)-[:LINK]->(R),   (R)-[:LINK]->(r4),
    (r1)-[:LINK]->(r2),  (r2)-[:LINK]->(r1),
    (r1)-[:LINK]->(r3),  (r3)-[:LINK]->(r1),
    (r1)-[:LINK]->(r4),  (r4)-[:LINK]->(r1),
    (r2)-[:LINK]->(r3),  (r3)-[:LINK]->(r2),
    (r2)-[:LINK]->(r4),  (r4)-[:LINK]->(r2),
    (r3)-[:LINK]->(r4),  (r4)-[:LINK]->(r3),
    (j1)-[:LINK]->(J),   (J)-[:LINK]->(j1),
    (j2)-[:LINK]->(J),   (J)-[:LINK]->(j2),
    (j3)-[:LINK]->(J),   (J)-[:LINK]->(j3),
    (j4)-[:LINK]->(J),   (J)-[:LINK]->(j4),
    (j1)-[:LINK]->(j2),  (j2)-[:LINK]->(j1),
    (j1)-[:LINK]->(j3),  (j3)-[:LINK]->(j1),
    (j1)-[:LINK]->(j4),  (j4)-[:LINK]->(j1),
    (j2)-[:LINK]->(j3),  (j3)-[:LINK]->(j2),
    (j2)-[:LINK]->(j4),  (j4)-[:LINK]->(j2),
    (j3)-[:LINK]->(j4),  (j4)-[:LINK]->(j3) ;
----

This graph has two clusters of _Users_, that are closely connected.
Between those clusters there is bidirectional relationship.

We can now project the graph and store it in the graph catalog.

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

.The following statement will project the graph and store it in the graph catalog.
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project( 'graph', ['Male', 'Female'], 'LINK' );
----


=== Sampling

We can now go on to sample a subgraph from "myGraph" using CNARW.
Using the "Juliette" node as our set of start nodes, we will venture to visit five nodes in the graph for our sample.
Since we have six nodes total in our graph, and 5/10 = 0.5 we will use this as our sampling ratio.

[role=query-example, no-result=true]
--
.The following will run the Common Neighbour Aware random walk sampling algorithm:
[source, cypher, role=noplay]
----
MATCH (start:Female {name: 'Juliette'})
CALL gds.graph.sample.cnarw('sampledGraph', 'graph',
{
    samplingRatio: 0.5,
    startNodes: [id(start)]
})
YIELD nodeCount
RETURN nodeCount;
----

.Results
[opts="header"]
|===
| nodeCount
| 5
|===
--

Due to the random nature of the sampling the algorithm may return different counts in different runs.

The main difference between the Common Neighbour Aware Random Walk and Random Walks with Restarts graphs sampling algorithms is that there are more chances to go into another cluster for the first one, which is colored in blue in the example above.
The relationships sampled are those connecting these nodes.

[[cnarw-examples-memory-estimation]]
=== Memory Estimation

First off, we will estimate the cost of running the algorithm using the `estimate` procedure.
This can be done with any execution mode.
Estimating the sampling procedure is useful to understand the memory impact that running the procedure on your graph will have.
If the estimation shows that there is a very high probability of the execution going over its memory limitations, the execution is prohibited.
To read more about this, see xref:common-usage/memory-estimation.adoc#estimate-heap-control[Automatic estimation and execution blocking].

For more details on `estimate` in general, see xref:common-usage/memory-estimation.adoc[Memory Estimation].

[role=query-example]
--
.The following will estimate the memory requirements for running the sampling algorithm:
[source, cypher, role=noplay]
----
CALL gds.graph.sample.cnarw.estimate('graph',
{
    samplingRatio: 0.5
})
YIELD requiredMemory
RETURN requiredMemory;
----

.Results
[opts="header"]
|===
| requiredMemory
| "1264 Bytes"
|===
--
